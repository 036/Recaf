import com.github.benmanes.gradle.versions.VersionsPlugin

plugins {
    id "com.github.ben-manes.versions" version "0.42.0"
}

allprojects {
    group 'me.coley'
    version '3.0.0-SNAPSHOT'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'checkstyle'
    apply plugin: VersionsPlugin

    // https://docs.gradle.org/current/userguide/toolchains.html
    // gradlew -q javaToolchains - see the list of detected toolchains.
    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(11)
        }
    }

    checkstyleMain.exclude '**/generated-src/**'

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
        options.incremental = true
    }

    repositories {
        mavenCentral()
        mavenLocal()
        google()
        maven { url 'https://files.minecraftforge.net/maven' }
        maven { url 'https://jitpack.io' }
        maven { url 'https://maven.quiltmc.org/repository/release/' }
    }

    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }

    project.ext {
        def asmVersion = '9.3'
        def dexVersion = '2.5.2'
        def dudeVersion = '1.10.0'
        def jadxVersion = '1.3.3'
        def jpVersion = '3.24.2'
        def junitVersion = '5.8.2'
        def lljzipVersion = '1.1.5'
        def ssvmVersion = '1.7.1'
        def quiltflowerVersion = '1.8.1'
        aapt = 'com.android.tools.build:aapt2-proto:7.1.2-7984345'
        asm = "org.ow2.asm:asm:$asmVersion"
        asm_analysis = "org.ow2.asm:asm-analysis:$asmVersion"
        asm_commons = "org.ow2.asm:asm-commons:$asmVersion"
        asm_tree = "org.ow2.asm:asm-tree:$asmVersion"
        asm_util = "org.ow2.asm:asm-util:$asmVersion"
        baksmali = "org.smali:baksmali:$dexVersion"
        beanshell = 'com.github.Col-E:beanshell:3.0.0-1'
        cafedude = "com.github.Col-E:CAFED00D:$dudeVersion"
        cfr = 'org.benf:cfr:0.152'
        common_io = 'commons-io:commons-io:2.11.0'
        dexlib = "org.smali:dexlib2:$dexVersion"
        directories = 'dev.dirs:directories:26'
        docking = 'com.github.Col-E:tiwulfx-dock:1.2.3'
        gson = 'com.google.code.gson:gson:2.9.0'
        guava = 'com.google.guava:guava:31.0.1-jre'
        hppc = 'com.carrotsearch:hppc:0.9.1'
        jadx = "io.github.skylot:jadx-core:$jadxVersion"
        jadx_input = "io.github.skylot:jadx-java-input:$jadxVersion"
        java_parser = "com.github.javaparser:javaparser-symbol-solver-core:$jpVersion"
        javassist = 'org.javassist:javassist:3.28.0-GA'
        javax_annos = 'javax.annotation:javax.annotation-api:1.3.2'
        jelf = 'net.fornwall:jelf:0.7.0'
        jphantom = 'com.github.Col-E:jphantom:1.4.3'
        junit_api = "org.junit.jupiter:junit-jupiter-api:$junitVersion"
        junit_engine = "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
        junit_params = "org.junit.jupiter:junit-jupiter-params:$junitVersion"
        llzip = "com.github.Col-E:LL-Java-Zip:$lljzipVersion"
        logback_classic = 'ch.qos.logback:logback-classic:1.2.10'
        pe = 'com.github.cademtz:JavaPeParser:1.0.0'
        picocli = 'info.picocli:picocli:4.6.3'
        quiltflower = "org.quiltmc:quiltflower:$quiltflowerVersion"
        regex = 'net.sourceforge.jregex:jregex:1.2_01'
        richtextfx = 'org.fxmisc.richtext:richtextfx:0.10.9'
        smali = "org.smali:smali:$dexVersion"
        ssvm = "com.github.xxdark:ssvm:$ssvmVersion"
    }
}
